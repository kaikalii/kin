WHITESPACE = _{ " " | "\t" | "\r" }
ws = { WHITESPACE }
single_line_comment = @{ "--" ~ (!NEWLINE ~ ANY)* ~ &(NEWLINE | EOI) }
multi_line_comment = @{ "'''" ~ (!"'''" ~ ANY)* ~ ("'''" | &EOI) }
COMMENT = _{ multi_line_comment | single_line_comment }
keyword = { "true" | "false" | "nil" | "and" | "or" | "not" | "end" }

// Numbers
int = @{ ("+" | "-")? ~ ASCII_DIGIT+ }
real = @{ int ~ ("." ~ ASCII_DIGIT+) ~ (^"e" ~ int)? }

// String literals
string = ${ "\"" ~ (raw_string | "\\" ~ (predefined | byte | unicode))* ~ "\"" }
raw_string = @{ (!("\\" | "\"") ~ ANY)+ }
hex = _{ '0'..'9' | 'a'..'f' | 'A'..'F' }
unicode_hex = @{ hex{1, 6} }
predefined = { "n" | "r" | "t" | "\\" | "0" | "\"" | "'" }
byte = @{ "x" ~ hex{2} }
unicode = @{ "u" ~ "{" ~ unicode_hex ~ "}" }

// Idents
ident_init = _{ LETTER | OTHER_SYMBOL }
ident_tail = _{ ident_init | DECIMAL_NUMBER | OTHER_NUMBER }
ident_inner = @{ ident_init ~ ("_"* ~ ident_tail+)* }
starts_with_keyword = @{ keyword ~ ("_"* ~ ident_tail+)+ }
ident = { !keyword ~ ident_inner | starts_with_keyword | "_" }

// Operators
op_as = { "+" | "-" }
op_mdr = { "*" | "/" | "%" }
op_comp = { "==" | "!=" | "<=" | ">=" | "<" | ">" }
op_and = { "and" }
op_or = { "or" }
op_un = { "not" | "-" }

// Expressions
paren_expr = { "(" ~ NEWLINE? ~ items ~ NEWLINE? ~ ")" }
bool_literal = { "true" | "false" }
nil = { "nil" }
param = { ident }
closure_params = { (param | "|" ~ param*) ~ "|" }
closure = { closure_params ~ (NEWLINE ~ items | expr)}
term = { real | int | closure | ident | bool_literal | nil | string | paren_expr }
expr_call_single = { term+ }
chain_call = _{ "," ~ NEWLINE? }
expr_call = { expr_call_single ~ (chain_call ~ expr_call_single)* }
expr_un = { op_un? ~ expr_call }
expr_mdr = { expr_un ~ (op_mdr ~ expr_un)* }
expr_as = { expr_mdr ~ (op_as ~ expr_mdr)* }
expr_cmp = { expr_as ~ (op_comp ~ expr_as)* }
expr_and = { expr_cmp ~ (NEWLINE? ~ op_and ~ NEWLINE? ~ expr_cmp)* }
expr_or = { expr_and ~ (NEWLINE? ~ op_or ~ NEWLINE? ~ expr_and)* }
expr = { expr_or }

// Items
equals = { "=" }
def = { ident ~ param* ~ equals ~ (NEWLINE ~ items ~ "end" | expr) }
item = { def | expr }
items = { (item ~ NEWLINE*)+ }
file = { SOI ~ NEWLINE* ~ items? ~ EOI }
